def rust_library(name, srcs, deps=None, visibility=None):
    """Defines a build rule for a Rust library.

    Args:
      name: Name of the rule.
      srcs: Source files for this library.
      deps: Dependencies of this rule needed for it to compile.
      visibility: Visibility declaration.
    """
    cmd = '%s --crate-type=lib $SRCS -o $OUT' % CONFIG.RUSTC_TOOL
    build_rule(
        name = name,
        srcs = srcs,
        outs = ['lib%s.rlib' % name],
        cmd = {
            'dbg': cmd + ' -g',
            'opt': cmd + ' -O',
        },
        deps = deps,
        visibility = visibility,
    )


def rust_binary(name, main, deps=None, visibility=None):
    """Defines a build rule for a Rust standalone binary.

    Args:
      name: Name of the rule.
      main: Source files containing the main() function.
      deps: Dependencies of this rule needed for it to compile.
      visibility: Visibility declaration.
    """
    cmd = '%s $SRCS -o $OUT' % CONFIG.RUSTC_TOOL
    build_rule(
        name = name,
        srcs = [main],
        outs = [name],
        cmd = {
            'dbg': cmd + ' -g',
            'opt': cmd + ' -O',
        },
        deps = deps,
        visibility = visibility,
    )


# Add the RUSTC_TOOL to the config so people can override with package().
# In future it would be nice to be able to configure this kind of thing
# in plzconfig.
CONFIG.setdefault('RUSTC_TOOL', 'rustc')
